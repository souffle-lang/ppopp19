# Copyright (c) 2016, Oracle and/or its affiliates. All rights reserved.
#
# The Universal Permissive License (UPL), Version 1.0
#
# Subject to the condition set forth below, permission is hereby granted to any person obtaining a copy of this software,
# associated documentation and/or data (collectively the "Software"), free of charge and under any and all copyright rights in the 
# Software, and any and all patent rights owned or freely licensable by each licensor hereunder covering either (i) the unmodified 
# Software as contributed to or provided by such licensor, or (ii) the Larger Works (as defined below), to deal in both
# 
# (a) the Software, and
# (b) any piece of software and/or hardware listed in the lrgrwrks.txt file if one is included with the Software (each a “Larger
# Work” to which the Software is contributed by such licensors),
#
# without restriction, including without limitation the rights to copy, create derivative works of, display, perform, and 
# distribute the Software and make, use, sell, offer for sale, import, export, have made, and have sold the Software and the 
# Larger Work(s), and to sublicense the foregoing rights on either these or other terms.
#
# This license is subject to the following condition:
# The above copyright notice and either this complete permission notice or at a minimum a reference to the UPL must be included in 
# all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES
# OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE
# LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR
# IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.

dnl Define all possible souffle-profile commands for testing
m4_define([PROFILE_COMMANDS],[["rel"],
  ["rel R2"],
  ["rul"],
  ["rul C2.1"],
  ["rul id N2.1"],
  ["graph R2 copy_t"],
  ["graph C2.1 tot_t"],
  ["graph ver C2.1 tuples"],
  ["top"],
  ["help"]])

dnl Execute a test case with profiling, and check the profile generated is as expected
dnl $1 -- test case
dnl $2 -- category
dnl $3 -- command
m4_define([TEST_PROFILE_COMMAND],[
  m4_define([TESTNAME],[$1])
  m4_define([CATEGORY],[$2])
  m4_define([CONF],[$3])
  m4_define([TESTDIR],["$TESTS"/CATEGORY/TESTNAME])
  m4_define([LOG_FILE],[$1-profile.log])
  m4_define([PROGRAM],[TESTDIR/TESTNAME.dl])
  m4_define([FACTS],[TESTDIR/facts])
  m4_define([EXPECTED_OUTPUT],["TESTDIR/out/$4.out"])
  AT_CHECK(["$SOUFFLE" CONF -D. -p LOG_FILE -F FACTS PROGRAM 1>TESTNAME.out 2>TESTNAME.err], [0])
  FILE_EXISTS([LOG_FILE])
  AT_CHECK(["$SOUFFLE_PROFILE" LOG_FILE -c $4 1>TESTNAME.prof0.out 2>TESTNAME.prof.err], [0])
  AT_CHECK([sed 's?[./].*$1.dl?$1.dl?g' TESTNAME.prof0.out >TESTNAME.prof.out], [0])
  SAME_LINE_COUNT([TESTNAME.prof.out],[EXPECTED_OUTPUT])
])

dnl Execute a set of tests on the profiler
dnl $1 -- test case
dnl $2 -- category
m4_define([PROFILE_TEST],[
  m4_foreach([FLAGS],[CONFS],[
    m4_foreach([COMMAND],[PROFILE_COMMANDS],[
      AT_SETUP([$1 FLAGS souffle-profile -c COMMAND])
      TEST_PROFILE_COMMAND([$1],[$2],[FLAGS],[COMMAND])
      AT_CLEANUP([])
    ])
  ])
])

##########################################################################

PROFILE_TEST([lrg_attr_id],[profile])
PROFILE_TEST([recursive],[profile])
